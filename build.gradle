plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.21.0'
    id 'maven-publish'
}

apply from: 'version.gradle'

repositories {
    mavenCentral()
}

version = pluginVersion
group = 'com.github.sahara3'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    compileOnly 'org.apache.tomcat.embed:tomcat-embed-core:8.0.53'

    implementation 'org.apache.commons:commons-lang3:3.14.0'
}

gradlePlugin {
    plugins {
        tomcatRunnerPlugin {
            id = 'com.github.sahara3.tomcat-runner'
            implementationClass = 'com.github.sahara3.gradle.tomcat.TomcatRunnerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/sahara3/gradle-tomcat-runner-plugin'
    vcsUrl = 'https://github.com/sahara3/gradle-tomcat-runner-plugin.git'
    description = 'Gradle plugin for running Apache Tomcat'
    tags = ['tomcat']

    plugins {
        tomcatRunnerPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Tomcat Runner plugin'
        }
    }
}

if (version.endsWith('-SNAPSHOT')) {
    tasks.publishPlugins.enabled = false
    tasks.publish.enabled = false

    publishing {
        publications {
            pluginPublication(MavenPublication) {
                from components.java
            }
        }
    }
}

tasks.clean.doLast {
    project.delete 'bin'
}
