buildscript {
    apply from: '../version.gradle'
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "com.github.sahara3:gradle-tomcat-runner-plugin:${pluginVersion}"
    }
}

configure(allprojects) {
    apply plugin: 'com.github.sahara3.tomcat-runner'

    repositories {
        mavenCentral()
    }
}

// each test case.
configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'war'

    dependencies {
        providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    }
}

// all combination.
unzipWebapps.dependsOn ':myapp1:war', ':myapp2:war', ':myapp3:war', ':myapp4:war'

tomcat {
    version = '10.1.19'
    port = 8080
    systemProperty 'your.custom.property', 'property-value'

    webapp(project(':myapp1')) {
        contextPath = '' // root context.
    }

    webapp project(':myapp2')

    webapp 'myapp3/build/libs/myapp3.war'

    webapp file('myapp4/build/libs/myapp4.war')
}
